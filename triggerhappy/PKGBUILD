# Maintainer: Dryamov Vyacheslav <dryamov.dev@gmail.com>

pkgname=triggerhappy
pkgver=r173
pkgrel=1
pkgdesc="A lightweight hotkey daemon"
arch=('i686' 'x86_64')
url="https://github.com/wertarbyte/triggerhappy"
license=('GPL')
conflicts=('triggerhappy-git')
provides=('triggerhappy')
depends=('libsystemd')
makedepends=('git' 'linux-api-headers')
source=('git+https://github.com/Dryamov/triggerhappy')
md5sums=('SKIP')

pkgver() {
    cd "triggerhappy"
    printf "r%s" "$(git rev-list --count HEAD)"
}

build() {
    cd "$srcdir/triggerhappy"
    make
}

package() {
    cd "$srcdir/triggerhappy"
    make DESTDIR="$pkgdir/" install
   
    install -D -m 644 -t "$pkgdir/usr/lib/systemd/system" "systemd/triggerhappy.service"
    install -D -m 644 -t "$pkgdir/usr/lib/systemd/user"   "systemd/user/triggerhappy.service"
    install -D -m 644 -t "$pkgdir/usr/share/triggerhappy"   "triggers.conf"
    install -D -m 644 -T "udev/98-input.rules" "$pkgdir/usr/lib/udev/rules.d/98-input.rules"

    mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
}

