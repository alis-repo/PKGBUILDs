pkgname=(termite termite-terminfo)
pkgver=13.r9.g3dcf389
pkgrel=1

pkgdesc="A simple VTE-based terminal with no-csd patch"
url="https://github.com/thestinger/termite/"
arch=('i686' 'x86_64')
license=('LGPL')

makedepends=('git' 'vte3-ng' 'ncurses')

source=(git://github.com/thestinger/termite
        git+https://github.com/thestinger/util
        https://raw.githubusercontent.com/unit73e/aur-termite-nocsd/master/termite.patch
        )

md5sums=('SKIP'
         'SKIP'
         'd0a7aa26ca1ef40951726ac1a24bf0f1')

pkgver() {
  cd termite
  git describe --long --always | sed 's/^v//; s/-/.r/; s/-/./'
}

prepare() {
  cd termite
  git submodule init
  git config submodule.util.url "$srcdir"/util
  git submodule update
  patch -Np1 -i "${srcdir}/termite.patch" "${srcdir}/termite/termite.cc" 
}

build() {
  cd termite
  make
}

package_termite() {
  pkgdesc="A simple VTE-based terminal"
  depends=('vte3-ng' 'termite-terminfo')
  conflicts=('termite')
  backup=(etc/xdg/termite/config)

  cd termite
  make PREFIX=/usr DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/share/terminfo
  install -Dm644 config "$pkgdir"/etc/xdg/termite/config
}

package_termite-terminfo() {
  pkgdesc='Terminfo for Termite, a simple VTE-based terminal'
  conflicts=('termite-terminfo')

  cd termite
  install -d "$pkgdir"/usr/share/terminfo
  tic -x termite.terminfo -o "$pkgdir"/usr/share/terminfo
}
